plugins {
  id 'java-library'
  id 'maven-publish'
  id 'alkemy.publish-conventions'
  id 'signing'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.dokka'
  id 'jacoco'
}

group = 'io.resoluteworks'

repositories {
  mavenCentral()
}

java {
  withJavadocJar()
  withSourcesJar()
}

javadocJar {
  dependsOn(dokkaHtml)
  archiveClassifier.set('javadoc')
  from(dokkaHtml.outputDirectory)
}

dokkaHtml {
  outputDirectory.set(new File(rootDir, "docs/dokka/${name}"))
}

kotlin {
  jvmToolchain(8)
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true
  }
  systemProperty 'kotest.framework.parallelism', '4'
}

signing {
  required { !version.toString().contains("-dev-experimental") && gradle.taskGraph.hasTask("publish") }
  sign publishing.publications.mavenJava
}

jacocoTestReport {
  reports {
    html.required = true
    xml.required = true
  }
}

tasks.withType(PublishToMavenRepository).configureEach { it.dependsOn test }
