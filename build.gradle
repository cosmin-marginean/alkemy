plugins {
  id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false  // Kotlin Gradle plugin must be loaded in the parent/root project
  id 'com.glovoapp.semantic-versioning' version '1.1.10'
  id 'jacoco-report-aggregation'
  id 'com.github.nbaztec.coveralls-jacoco' version '1.2.15'
}

repositories {
  mavenCentral()
}

dependencies {
  subprojects.forEach { subproject ->
    jacocoAggregation project(":${subproject.name}")
  }
}

reporting {
  reports {
    testCodeCoverageReport(JacocoCoverageReport) {
      testType = TestSuiteType.UNIT_TEST
    }
  }
}

coverallsJacoco {
  reportPath = 'build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml'
  // workaround until https://github.com/nbaztec/coveralls-jacoco-gradle-plugin/pull/62 is merged and released
  reportSourceSets = configurations.allCodeCoverageReportSourceDirectories.incoming.artifactView {view -> {
    view.componentFilter(id -> id instanceof ProjectComponentIdentifier)
    view.lenient(true)
  }}.files.files
}

tasks.coverallsJacoco {
  mustRunAfter('testCodeCoverageReport')
}

tasks.register('fullBuild') {
  dependsOn 'testCodeCoverageReport'
  dependsOn 'coverallsJacoco'
}
